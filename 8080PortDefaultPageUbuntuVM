#Creates an Ubuntu VM in Azure.
#Enables port 8080 via a Network Security Group (NSG).
#Installs Nginx and configures it to serve a default web page on port 8080.
#Outputs the public IP address of the VM so you can access the website.


#azure provider 

provider "azurerm" {
  features {}
}

#Resource group

resource "azurerm_resource_group" "rg" {
  name     = "ubuntu-vm-rg"
  location = "East US"
}

#confing VNET

resource "azurerm_virtual_network" "vnet" {
  name                = "ubuntu-vnet"
  address_space       = ["10.0.0.0/16"]
  location            = azurerm_resource_group.rg.location
  resource_group_name = azurerm_resource_group.rg.name
}

#config subnet 

resource "azurerm_subnet" "subnet" {
  name                 = "ubuntu-subnet"
  resource_group_name  = azurerm_resource_group.rg.name
  virtual_network_name = azurerm_virtual_network.vnet.name
  address_prefixes     = ["10.0.1.0/24"]
}

#config nsg

resource "azurerm_network_security_group" "nsg" {
  name                = "ubuntu-nsg"
  location            = azurerm_resource_group.rg.location
  resource_group_name = azurerm_resource_group.rg.name
}

#allow SSH (port 22)
resource "azurerm_network_security_rule" "ssh" {
  name                        = "AllowSSH"
  priority                    = 100
  direction                   = "Inbound"
  access                      = "Allow"
  protocol                    = "Tcp"
  source_port_range           = "*"
  destination_port_range      = "22"
  source_address_prefix       = "*"
  destination_address_prefix  = "*"
  network_security_group_name = azurerm_network_security_group.nsg.name
  resource_group_name         = azurerm_resource_group.rg.name
}

# allow 8080 port

  resource "azurerm_network_security_rule" "allow_8080" {
    name                       = "Allow-8080"
    priority                   = 200
    direction                  = "Inbound"
    access                     = "Allow"
    protocol                   = "Tcp"
    source_port_range          = "*"
    destination_port_range     = "8080"
    source_address_prefix      = "*"
    destination_address_prefix = "*"
    network_security_group_name = azurerm_network_security_group.nsg.name
    resource_group_name         = azurerm_resource_group.rg.name

  }

# Associate NSG with Subnet

resource "azurerm_subnet_network_security_group_association" "nsg_assoc" {
  subnet_id                 = azurerm_subnet.subnet.id
  network_security_group_id = azurerm_network_security_group.nsg.id
}

#config nic

resource "azurerm_network_interface" "nic" {
  name                = "ubuntu-nic"
  location            = azurerm_resource_group.rg.location
  resource_group_name = azurerm_resource_group.rg.name

  ip_configuration {
    name                          = "internal"
    subnet_id                     = azurerm_subnet.subnet.id
    private_ip_address_allocation = "Dynamic"
    public_ip_address_id          = azurerm_public_ip.public_ip.id
  }
}

#config public ip address

resource "azurerm_public_ip" "public_ip" {
  name                = "ubuntu-public-ip"
  location            = azurerm_resource_group.rg.location
  resource_group_name = azurerm_resource_group.rg.name
  allocation_method   = "Dynamic"
}

#confing vm

resource "azurerm_linux_virtual_machine" "vm" {
  name                = "ubuntu-vm"
  resource_group_name = azurerm_resource_group.rg.name
  location            = azurerm_resource_group.rg.location
  size                = "Standard_B1s"
  admin_username      = "sysadmin"
  network_interface_ids = [
    azurerm_network_interface.nic.id,
  ]
  computer_name  = "ubuntu-vm"
  admin_password = "Password123#"
  disable_password_authentication = false

  os_disk {
    caching              = "ReadWrite"
    storage_account_type = "Standard_LRS"
    name                 = "ubuntu-osdisk"
  }

  source_image_reference {
    publisher = "Canonical"
    offer     = "0001-com-ubuntu-server-focal"
    sku       = "20_04-lts"
    version   = "latest"
  }

  custom_data = base64encode(<<EOF
#!/bin/bash
apt-get update
apt-get install -y nginx
sed -i 's/listen 80 default_server;/listen 8080 default_server;/g' /etc/nginx/sites-available/default
systemctl restart nginx
EOF
  )
}

output "public_ip_address" {
  value = azurerm_linux_virtual_machine.vm.public_ip_address
  description = "Public IP address of the Ubuntu VM"
}

output "website_url" {
  value = "http://${azurerm_linux_virtual_machine.vm.public_ip_address}:8080"
 # curl "http://${azurerm_linux_virtual_machine.vm.public_ip_address}:8080"
  description = "URL to access the default website"
}
